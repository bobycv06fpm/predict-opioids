Import("*")

features = []
manifests = []

def Feature(name, deps, *args):
    """
    Helper function for succinctly specifying a feature file and
    its arguments.
    """
    env.Python(["#scratch/features/{}.csv".format(name),
                "#output/features/{}.manifest".format(name)], 
               ["{}.py".format(name)] + [tables[name] for name in deps] + list(args))
    env.Python("#scratch/features/{}.normalized.csv".format(name),
               ["normalize.py", "#scratch/populations/InitialRX.csv", "#scratch/features/{}.csv".format(name)],
               log_path="#output/features/{}.normalize.log".format(name))
    features.append("#scratch/features/{}.normalized.csv".format(name))
    manifests.append("#output/features/{}.manifest".format(name))


Feature("DLT/naics",
        ["population", "lookback", "dlt_wage"])
Feature("DLT/unemp_ri",
        ["population", "lookback"],
        constants["NSTEPS"],
        "#input/public/ri_month_unemp.csv")
Feature("DLT/unemp_naics",
        ["population", "lookback", "dlt_wage"],
        "#input/public/nat_naics_unemp.csv")
Feature("DLT/wages",
        ["population", "lookback", "dlt_wage"])
Feature("DOC/events",
        ["population", "lookback", "dim_date", "doc_events"])
Feature("DOC/sentences",
        ["population", "lookback", "dim_date", "doc_sentences"])
#Feature("Medicaid/diag_cde",
#        ["population", "lookback", "dim_date", "medicaid_diag_cde", "diag_corr"],
#        "#input/public/CMS29_DESC_LONG_SHORT_DX_101111u021012.csv",
#        "#input/public/icd9-sections.csv")
Feature("Medicaid/enrollment",
        ["population", "lookback", "medicaid_enrollment_2", "dim_aid_ctg_cde", "eohhs_recip_demo", "eohhs_recip_x_ssn"])
Feature("Medicaid/payments",
        ["population", "lookback", "dim_date", "medicaid_claims", "medicaid_pharmacy"])
Feature("Medicaid/pharmacy",
        ["population", "lookback", "dim_date", "medicaid_pharmacy"],
        "#input/other/ashp.csv")
#Feature("Medicaid/proc_cde",
#        ["population", "lookback", "dim_date", "medicaid_proc_cde", "proc_corr"],
#        "#input/ri360/MEDICAID-DIM_PROC_CDE.csv")
Feature("Mega/addresses",
        ["population", "lookback", "mega_addr"])
Feature("Mega/births",
        ["population", "lookback", "mega_demo"])
Feature("Mega/demographics",
        ["population", "lookback", "mega_demo"])
Feature("Mega/payments",
        ["population", "lookback", "mega_pay"])

# Manifest

env.Command("#output/features/manifest.tsv",
            manifests,
            "cat $SOURCES > $TARGET")

features = {"all": features}
Export("features")

# vim: syntax=python expandtab sw=4 ts=4
