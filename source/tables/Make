Import("*")

# Table 1 - comparison of AUCs
env.Python("#output/tables/Table1.csv",
           ["Table1.py",
            constants["RANDOM_SEED"],
            constants["PLOT_BOOTSTRAPS"],
            "#output/models/postlasso.all.OUTCOME_ANY.y_pred.csv",
            "#output/models/bagged-lasso.all.OUTCOME_ANY.y_pred.csv",
            "#output/models/lstm.all.OUTCOME_ANY.y_pred.csv"])

# Table S1 - baseline characteristics
env.Python(["#scratch/tables/demographics.csv",
            "#scratch/tables/demographics.manifest"],
           ["demographics.py",
            tables["population"],
            tables["dim_date"],
            tables["mega_demo"]])
env.R("#output/tables/TableS1.tex",
      ["TableS1.R",
       "#scratch/outcomes/all.csv",
       "#scratch/tables/demographics.csv",
       "#scratch/features/Mega/census.csv",
       "#scratch/features/Mega/household.csv",
       "#scratch/features/Mega/payments.csv",
       "#scratch/features/DLT/wages.csv",
       "#input/public/ACS_15_5YR_B17010_with_ann.csv"])

# Table S2 - regression output
env.Python("#output/tables/TableS2.tex",
           ["TableS2.py",
            "#output/models/lasso.bootstrap.all.OUTCOME_ANY.freq.csv",
            "#output/models/postlasso.all.OUTCOME_ANY.csv"])

# Table S3 - low-dosage opioids
env.Python("#output/tables/TableS3.tex",
           ["TableS3.py",
            "#output/populations/CompareOpioids.csv"])

# Table S12 - neural net AUCs
tensors = ["DHS", "DLT", "DOC", "Medicaid", "Police", "all"]
env.Python("#output/tables/TableS12.tex",
           ["TableS12.py",
            constants["RANDOM_SEED"],
            constants["PLOT_BOOTSTRAPS"],
            Value(",".join(outcomes)),
            Value(",".join(tensors))] + \
           ["#output/models/lstm.{}.{}.y_pred.csv".format(tensor, outcome)
            for tensor in tensors
            for outcome in outcomes])

# vim: syntax=python expandtab sw=4 ts=4
