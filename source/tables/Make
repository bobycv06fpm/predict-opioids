Import("*")

# Table A3 - sample characteristics
env.Python(["#scratch/tables/demographics.csv",
            "#scratch/tables/demographics.manifest"],
           ["demographics.py",
            tables["population"],
            tables["dim_date"],
            tables["mega_demo"]])
env.R("#output/tables/TableA3.tex",
      ["TableA3.R",
       "#scratch/outcomes/all.csv",
       "#scratch/tables/demographics.csv",
       "#scratch/features/Mega/census.csv",
       "#scratch/features/Mega/household.csv",
       "#scratch/features/Mega/payments.csv",
       "#scratch/features/DLT/wages.csv",
       "#input/public/ACS_15_5YR_B17010_with_ann.csv"])

# Table A4 - BOLASSO selected varibales
env.Python("#output/tables/TableA4.tex",
           ["TableA4.py",
            "#output/models/bolasso.freq.all.OUTCOME_ANY.csv"])

# Table A5 - logit summary
env.Python("#output/tables/TableA5.tex",
           ["TableA5.py",
            "#output/models/postlasso.all.OUTCOME_ANY.csv"])

# Table A6 - neural net AUCs
tensors = ["DHS", "DLT", "DOC", "Medicaid", "Police", "all"]
env.Python("#output/tables/TableA6.tex",
           ["TableA6.py",
            constants["RANDOM_SEED"],
            constants["PLOT_BOOTSTRAPS"],
            Value(",".join(outcomes)),
            Value(",".join(tensors))] + \
           ["#output/models/lstm.{}.{}.y_pred.csv".format(tensor, outcome)
            for tensor in tensors
            for outcome in outcomes])

# vim: syntax=python expandtab sw=4 ts=4
